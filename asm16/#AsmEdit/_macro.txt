;──────── макросы НАЧАЛА программы
;Cls         ;очистка экрана
;Video       ;адрес видеобуфера в ES
;
;──────── макросы для ЗАВЕРШЕНИЯ программы и ОПРОСА клавиатуры
;Pause       ;пауза (нажатый символ в AL, если AL=0 то в AH -спецкод)
;Stop        ;завершение программы 
;Key         ;опрос клавиатуры без ожидания (если не нажато,то AX=0
             ;  AL=нажатый символ, для спецкода: AL=0, AH=спецкод)
;
;──────── макросы для ПЕЧАТИ СИМВОЛОВ, ТЕКСТА и ЧИСЕЛ
;Write   Z   ;печать символа Z
;Writeln     ;перевод курсора в начало следующей строки
;Text    Z   ;печать текста из переменной Z (до символа '$')
;Print   Z   ;печать байта (две 16-е цифры)
;Kursor  Y,X ;установка курсора (Y от 0 по 24, X от 0 по 79)
;
;──────── макросы прочие
;Obmen   Y,Z ;обмен байтов (кроме AH и AL)
;Delay   Z   ;задержка (до 65535)



;==========================================================================

;======== МАКРОСЫ для НАЧАЛА программы 

  ;--очистка экрана
CLS macro
  mov AX,3   
  int 10h
endm

  ;--настройка ES на видеобуфер
VIDEO macro
  mov AX,0B800h
  mov ES,AX
endm

;======== МАКРОСЫ для ЗАВЕРШЕНИЯ программы и ОПРОСА клавиатуры

  ;--пауза (нажатый символ в AL, если AL=0 то в AH -спецкод)
PAUSE macro
  mov AH,10h
  int 16h
endm

  ;--завершение программы 
STOP macro         
  mov AH,4Ch
  int 21h   
endm

    ;--опрос клавиатуры без приостановки
    ;          если ничего не нажато, то  AL=0 и AH=0
    ;          если нажат спецкод,    то  AL=0 и AH=спецкод  
KEY macro         
 Local Key1        ;локальная метка
    mov AH,11h     ;есть ли факт нажатия ?  (если не нажато, то флаг нуля 1)
    int 16h    
    mov AX,0       
    jz Key1        ;если не нажато (ZF=1), то сделать AX=0
    mov AH,10h     ;что нажато: нажатый символ в AL, если AL=0 то в AH -спецкод)
    int 16h    
  Key1:            ;если не нажато, то AX=0
endm

;======== МАКРОСЫ для ПЕЧАТИ СИМВОЛОВ, ТЕКСТА и ЧИСЕЛ

  ;--печать символа Z (из: AL, '#', 65, [BX], Y, Y[BX] и т.п.)
WRITE macro _Z
  push AX
  mov AL,_Z
  mov AH,0Eh     
  int 10h
  pop AX
endm

  ;--перевод курсора в начало следующей строки
WRITELN macro
  push AX
  mov AH,0Eh     
  mov AL,13
  int 10h
  mov AL,10
  int 10h
  pop AX
endm

  ;--печать текста из переменной Z (до первого символа '$')
TEXT macro _Z         
  push AX 
  push DX 
  mov DX,offset _Z
  mov AH,09h         
  int 21h            
  pop DX 
  pop AX 
endm

   ;--установка курсора (Y от 0 по 24, X от 0 по 79)
KURSOR macro _Y,_X
  push AX
  push BX
  push DX
  mov BH,0
  mov DH,_Y
  mov DL,_X
  mov AH,2
  int 10h
  pop DX
  pop BX
  pop AX
endm

   ;--печать байта (две 16-е цифры)
PRINT macro _Z
  push AX
  push BX
  push CX
  mov CL,_Z        ;печатаемое число (для младшей цифры)
  mov CH,CL        ;печатаемое число (для старшей цифры)
    shr CH,1       ;-сдвиг старшей цифры вправо на 4 разряда
    shr CH,1 
    shr CH,1 
    shr CH,1 
    and CL,0Fh     ;-очистка младшей цифры
  add CX,3030h     ;обе цифры в символы   
    mov BH,'9'     ;--1 способ прибавления 7:   флаг = '9' - цифра        
    cmp BH,CH      ;флаг переноса (1 если символ > '9',  0 если меньше)
     mov BL,0
     RCL BL,1      ;флаг переноса в BL
      mov AL,7
      mul BL       ; AX=AL*7 = флаг*7
  add AL,CH        ;  +7 или +0
  write AL         ;печать символа из AL
    cmp CL,3Ah     ;--2 способ прибавления 7 :флаг переноса (0 если символ >= '9', 1 если меньше)
    CMC            ;инверсия переноса
     mov BL,0
     rcl BL,1      ;флаг переноса в BL
      mov AL,7
      mul BL       ; AX=AL*7 = флаг*7
  add AL,CL        ;  +7 или +0
  write AL         ;печать символа из AL
  pop CX
  pop BX
  pop AX
endm

;======== МАКРОСЫ прочие

   ;--обмен байтов (ячеек) -кроме AH и AL 
OBMEN macro _Y,_X
  push AX
  mov AH,_Y
  mov AL,_X
  mov _Y,AL
  mov _X,AH
  pop AX
endm

   ;--задержка (до 65535)
DELAY macro _Z
 Local Delay1
 Local Delay2
  push CX
  push DX
  mov DX,_Z
Delay1:
  mov CX,0FFFFh
Delay2:
  loop Delay2
  dec DX
  jnz Delay1
  pop DX
  pop CX
endm
