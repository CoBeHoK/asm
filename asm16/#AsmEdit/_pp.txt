;──────── подпрограммы РИСОВАНИЯ в видеобуфере
;
;call Line   ;ЛИНИЯ            BX -адрес начала (будет адрес последней точки)
;                              DX -направление: 2,-2,160,-160     AL -символ
;                              CL -длина (кол-во точек)           AH -цвет
;
;call Ramka  ;РАМКА            BX -адрес левого верха 
;                              CH -размер по вертикали            AL  -символ
;                              CL -размер по горизонтали          AH  -цвет
;
;call Box    ;ПРЯМОУГОЛЬНИК    BX -адрес левого верха 
;                              CH -размер по вертикали            AL  -символ
;                              CL -размер по горизонтали          AH  -цвет
;
;──────── подпрограммы ПЕЧАТИ 16-ричных чисел
;call Print_AL               ;печать БАЙТА из AL (две    16-е цифры)
;call Print_AX               ;печать СЛОВА из AX (четыре 16-е цифры)
;
;──────── подпрограммы ПЕЧАТИ десятичного числа из AL
;call Decimal                ;печать БЕЗ ПРЕДНУЛЕЙ:     '7'
;call Decimal0               ;печать C ПРЕДНУЛЯМИ:      '007'
;call Decimal_               ;печать C ПРЕДПРОБЕЛАМИ:   '..7'

;==========================================================================

;------ ЛИНИЯ 
          ; из BX -адрес начала (возвращает адрес последней точки)
          ; из DX -направление (вправо 2, влево -2, вниз 160, вверх -160)
          ; из CL -длина (кол-во точек)
          ; из AL -символ
          ; из AH -цвет
LINE:
  push AX
  push CX
                ;если push BX, то будет возвращать адрес начала линии
Line1:
  mov ES:[BX],AX   ;рисование "точки" (символ и цвет)
  add BX,DX        ;пересчет экранного адреса на DX
  dec CL           ;уменьшение счетчика
  jnz Line1        ;если линия не дорисована, то продолжать цикл
  sub BX,DX        ;шаг назад
                ;если pop BX,  то будет возвращать адрес начала линии
  pop CX
  pop AX           
  ret              ;линия нарисована, выход из подпрограммы


;------ РАМКА
          ; из BX  -адрес левого верха 
          ; из CH  -кол-во строк рамки 
          ; из CL  -кол-во позиций рамки
          ; из AL  -символ рамки
          ; из AH  -фон и цвет
RAMKA:
  push CX
  push DX

          ;линия вправо (CL точек)
  mov DX,2         ;направление (+2)
  call Line        ;рисование линии
          ;линия вниз   (CH точек)
  xchg CH,CL       ;кол-во точек
  mov DX,160       ;направление (+160)
  call Line
          ;линия влево (CL точек)
  xchg CH,CL       ;кол-во точек
  mov DX,-2        ;направление (-2)
  call Line
          ;линия вверх (CH точек)
  xchg CH,CL       ;кол-во точек
  mov DX,-160      ;направление (-160)
  call Line

  pop DX
  pop CX
  ret

;------ ПРЯМОУГОЛЬНИК закрашенный
          ; из BX  -адрес левого верха 
          ; из CH  -размер по вертикали   KY
          ; из CL  -размер по горизонтали KX  
          ; из AL  -символ рисования  
          ; из AH  -фон и цвет рисования
BOX:
  push AX
  push BX
  push CX
  push DX
            ;рисование DH строк
  mov DH,CH         ;временный регистр для KY
Box1:                    ;--CH строк        
            ;рисование строки из DL позиций
  mov DL,CL         ;временный регистр для KX
  push BX           ;запомнить начало строки
Box2:                   
  mov ES:[BX],AX    ;символ и цвет в ячейки
  add BX,2          ;к следующей ячейке
  dec DL            ;уменьшение счетчика позиций
  jnz Box2
  pop BX            ;восстановиnm адрес начала строки

  add BX,160        ;на строку ниже
  dec DH            ;уменьшение счетчика строк
  jnz Box1 

  pop DX
  pop CX
  pop BX
  pop AX
  ret

;==========================================================================

;------  ПЕЧАТЬ БАЙТА из AL (две 16-е цифры)
PRINT_AL:
   push AX
   push BX
   push CX
  mov CL,AL    ;печатаемое число
  lea BX,Put_  ;адрес таблицы или mov BX,offset Put_
    shr AL,1   ;сдвиг старшей цифры  (индекс)
    shr AL,1
    shr AL,1
    shr AL,1
  XLAT         ;в AL из [BX][AL]
  mov AH,0Eh   ;печать символа из AL
  int 10h 
    mov AL,CL    ;печатаемое число
    and AL,0Fh   ;очистка мл.цифры (индекс)
  XLAT         ;в AL из [BX][AL]
  mov AH,0Eh   ;печать символа из AL
  int 10h 
   pop CX
   pop BX
   pop AX
   ret
 Put_ db "0123456789ABCDEF" 

;------ печать СЛОВА из AX (четыре 16-е цифры)
PRINT_AX:
  xchg AH,AL
  call Print_AL
  xchg AH,AL
  call Print_AL
  ret

;==========================================================================

;------ печать ДЕСЯТИЧНОГО числа из AL - БЕЗ ПРЕДНУЛЕЙ:   '3'
DECIMAL:
  push AX  
  push CX  
  push DX
  call DEC_Reg            ;--формирование регистров CH,CL,DH

  cmp CH,'0'
  jnz DecMet1    ;если старшая цифра не 0, то печатать все три цифры
                 ;если старшая цифра 0, то анализ средней цифры
  cmp CL,'0'
  jnz DecMet2    ;если средняя цифра не 0, то печатать две младшие цифры
  jmp DecMet3    ;младшую цифру печатать в любом случае

                          ;--печать цифр из регистров CH,CL,DH
DecMet1:
  mov AL,CH     ;печать старшей цифры 
  mov AH,0Eh         
  int 10h
DecMet2:
  mov AL,CL     ;печать средней цифры 
  mov AH,0Eh         
  int 10h
DecMet3:
  mov AL,DH     ;печать младшей цифры
  mov AH,0Eh         
  int 10h

  pop DX   
  pop CX   
  pop AX
  ret

;------ печать ДЕСЯТИЧНОГО числа из AL - C ПРЕДНУЛЯМИ:   '003'
DECIMAL0:
  push AX
  push CX
  push DX
  call DEC_Reg            ;--формирование регистров CH,CL,DH
  jmp DecMet1             ;--на печать всех трех цифр из регистров CH,CL,DH


;------ печать ДЕСЯТИЧНОГО числа из AL - C ПРЕДПРОБЕЛАМИ:    '..3'
DECIMAL_:
  push AX
  push CX
  push DX
  call DEC_Reg            ;--формирование регистров CH,CL,DH
                          ;--опробелить преднули в регистрах

  cmp CH,'0'     ;старшая цифра=0 ?
  jnz DecMet1    ;если нет, то печать всех цифр
  mov CH,' '     ;если да, заменить '0' пробелом

  cmp CL,'0'     ;средняя цифра=0 ?
  jnz DecMet1    ;если нет, то печать всех цифр
  mov CL,' '     ;если да, заменить '0' пробелом
  jmp DecMet1    ;на печать всех трех цифр

;вспомогат.подпрограмма для формирования регистров CH,CL,DH (три 10-е цифры)
DEC_Reg:           
  mov AH,0           
  mov DL,100     ;делить на 100
  div DL         ;старшая цифра AL=2,AH=34
  mov CH,AL
  add CH,30h     ;старшая цифра - в CH

  mov AL,AH      ;остаток /100 равен 34
  mov AH,0
  mov DL,10      ;делить на 10
  div DL         ;средняя цифра AL=3,AH=4
  mov CL,AL          
  add CL,30h     ;средняя цифра - в CL

  mov DH,AH          
  add DH,30h     ;младшая цифра - в DH
  ret
